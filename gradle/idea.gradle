// Gradle IDEA project configuration stuff

idea {
    project {
        vcs = 'Git'

        ipr {
            withXml {
                def projectXml = it.asNode()
                customizeCodeStyle(projectXml, file("config/idea-codestyle.xml").getText("UTF-8"))
                customizeEncoding(projectXml)
            }
        }
    }

    module {
        name = rootProject.name

        downloadJavadoc = true
        downloadSources = true
    }
}

// Helpers

static findComponent(node, name) {
    node.component.find { it.@name == name }
}

static customizeCodeStyle(project, codeStyle) {
    project.appendNode('component', [name: 'ProjectCodeStyleSettingsManager'])
    def codeStyleNode = findComponent(project, 'ProjectCodeStyleSettingsManager')

    codeStyleNode.appendNode('option', [name: 'USE_PER_PROJECT_SETTINGS', value: 'true'])
    def projectSettingsNode = codeStyleNode.appendNode('option', [name: 'PER_PROJECT_SETTINGS'])
            .appendNode('value')

    //noinspection GroovyAssignabilityCheck
    def codeStyleXml = new XmlParser().parseText(codeStyle)
    codeStyleXml.children().each { option ->
        projectSettingsNode.append(option)
    }
}

static customizeEncoding(project) {
    def encoding = findComponent(project, 'Encoding')

    if (encoding) {
        // Change existing node.
        encoding.@useUTFGuessing = true
        encoding.@native2AsciiForPropertiesFiles = true
        encoding.@defaultCharsetForPropertiesFiles = 'UTF-8'
    } else {
        // Create new node with default values.
        project.appendNode 'Encoding',
                [useUTFGuessing                  : true,
                 native2AsciiForPropertiesFiles  : true,
                 defaultCharsetForPropertiesFiles: 'UTF-8']
    }
}
