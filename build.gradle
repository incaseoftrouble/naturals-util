plugins {
    id 'io.codearte.nexus-staging' version '0.9.0'
    id  'com.github.spotbugs' version '1.4'
}

group 'de.tum.in'
version '0.6.0'

apply plugin: 'java'

spotbugs {
    toolVersion = '3.1.0-RC6'
}
apply plugin: 'pmd'
apply plugin: 'checkstyle'

def defaultEncoding = 'UTF-8'
tasks.withType(JavaCompile) { options.encoding = defaultEncoding }
tasks.withType(Javadoc) { options.encoding = defaultEncoding }

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.0-RC6'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    compile group: 'it.unimi.dsi', name: 'fastutil', version:'8.1.0'
    // compile group: 'com.zaxxer', name: 'SparseBitSet', version:'1.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version: '2.3'

    testCompile 'org.openjdk.jmh:jmh-core:1.19'
    testCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
}

// Spotbugs
// https://plugins.gradle.org/plugin/com.github.spotbugs
// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.FindBugs.html
spotbugs {
    reportsDir = file("$project.buildDir/reports/spotbugs")
    effort = "max"
    reportLevel = "low"
    excludeFilter = file("$project.projectDir/config/spotbugs-exclude.xml")
    ignoreFailures = false
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// PMD
// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Pmd.html
pmd {
    toolVersion = "5.8.1"
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("$project.rootDir/config/pmd-rules.xml")
    ruleSets = [] // We specify all rules in rules.xml
    consoleOutput = false
    ignoreFailures = false
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// Checkstyle
// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Checkstyle.html
checkstyle {
    toolVersion = "8.2"
    configFile = file("$project.rootDir/config/checkstyle.xml")
    ignoreFailures = false
    maxWarnings = 0
    showViolations = false // Don't litter console
    checkstyleMain {
        configProperties = ['suppression-file':
                                    "$project.rootDir/config/checkstyle-main-suppression.xml"]
    }
    checkstyleTest {
        configProperties = ['suppression-file':
                                    "$project.rootDir/config/checkstyle-test-suppression.xml"]
    }
}

// Deployment - run with -Prelease clean uploadArchives closeAndReleaseRepository
if (project.hasProperty('release')) {
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'io.codearte.nexus-staging'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'naturals-util'
                    packaging 'jar'
                    description 'Datastructures and utility classes for non-negative integers'
                    url 'https://github.com/incaseoftrouble/naturals-util'

                    scm {
                        connection 'scm:git:https://github.com/incaseoftrouble/naturals-util.git'
                        developerConnection 'scm:git:git@github.com:incaseoftrouble/naturals-util.git'
                        url 'https://github.com/incaseoftrouble/naturals-util'
                    }

                    licenses {
                        license {
                            name 'The GNU General Public License, Version 3'
                            url 'https://www.gnu.org/licenses/gpl.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'incaseoftrouble'
                            name 'Tobias Meggendorfer'
                            email 'tobias@meggendorfer.de'
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        username = ossrhUsername
        password = ossrhPassword
        delayBetweenRetriesInMillis = 4000
    }
}
