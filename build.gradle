plugins {
    // https://plugins.gradle.org/plugin/io.codearte.nexus-staging
    id 'io.codearte.nexus-staging' version '0.21.0'
}

group 'de.tum.in'
version '0.13.0'

apply plugin: 'java'

apply plugin: 'pmd'
apply plugin: 'checkstyle'

apply plugin: 'idea'

project.sourceCompatibility = JavaVersion.VERSION_1_8
project.targetCompatibility = JavaVersion.VERSION_1_8

def defaultEncoding = 'UTF-8'
tasks.withType(JavaCompile) { options.encoding = defaultEncoding }
tasks.withType(Javadoc) { options.encoding = defaultEncoding }

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.2.2'
    implementation group: 'com.zaxxer', name: 'SparseBitSet', version: '1.2'
    implementation group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.8.5'
    // 0.8.6 is broken

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version: '2.3'
}

apply from: 'gradle/analysis.gradle'

// Deployment - run with -Prelease clean uploadArchives closeAndReleaseRepository
if (project.hasProperty('release')) {
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'io.codearte.nexus-staging'
    apply from: 'gradle/publish.gradle'
}

// If a user file is present, include it.
if (file('../user.gradle').exists()) {
    apply from: '../user.gradle'
}